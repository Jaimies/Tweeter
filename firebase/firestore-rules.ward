type User = {
    readonly username: string
    readonly email: string
    name: string
    bio: string
    following: string[]
}

type TweetAuthor = {
    id: string
    username: string
    name: string
}

type Tweet = {
    body: string
    author: TweetAuthor
    date: timestamp

    allow create: if canCreateTweet(data, getUser())
}

function canCreateTweet(data, user) {
    return data.date == request.time
            && data.author.name == user.name
            && data.author.username == user.username
}

function isLoggedIn(userId) {
    return request.auth != null && request.auth.uid == userId
}

function getUser() {
    return get(/databases/$(database)/documents/users/$(request.auth.uid)).data
}

match /users/{userId} is User {
    allow read: if true
    allow create, update: if isLoggedIn(userId)
}

match /tweets/{tweetId} is Tweet {
    allow read: if true
    allow create: if isLoggedIn(request.resource.data.author.id)
}
