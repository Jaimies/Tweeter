type User = {
    readonly username: string
    readonly username_lowercase: string
    readonly email: string
    name: string
    bio: string
    following: string[]

    allow create: if data.username_lowercase == data.username.lower()
}

type TweetAuthor = {
    id: string
    username: string
    name: string
}

type Tweet = {
    readonly body: string
    readonly author: TweetAuthor
    readonly date: timestamp
    likedBy: string[]

    allow update: if canUpdateTweet(data, prev)
    allow create: if canCreateTweet(data, getUser())
}

function canUpdateTweet(data, prev) {
    return likedByDiffersByUserId(data, prev) || likedByDiffersByUserId(prev, data)
}

function likedByDiffersByUserId(data, otherData) {
     return data.likedBy.toSet().difference(otherData.likedBy.toSet()) == [request.auth.uid].toSet() &&
            data.likedBy.toSet().intersection(otherData.likedBy.toSet()) == otherData.likedBy.toSet()
}

function canCreateTweet(data, user) {
    return data.date == request.time
            && data.author.name == user.name
            && data.author.username == user.username
            && data.likedBy == []
}

function isLoggedIn(userId) {
    return request.auth != null && request.auth.uid == userId
}

function getUser() {
    return get(/databases/$(database)/documents/users/$(request.auth.uid)).data
}

match /users/{userId} is User {
    allow read: if true
    allow create, update: if isLoggedIn(userId)
}

match /tweets/{tweetId} is Tweet {
    allow read: if true
    allow update: if true
    allow create: if isLoggedIn(request.resource.data.author.id)
}
